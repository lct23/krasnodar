(uiop:define-package #:app/games/guess-position
  (:use #:cl)
  (:import-from #:reblocks/widget
                #:update
                #:render
                #:defwidget)
  (:import-from #:reblocks/html
                #:with-html)
  (:import-from #:reblocks/dependencies
                #:get-dependencies)
  (:import-from #:reblocks-auth/models
                #:get-current-user
                #:get-all-users)
  (:import-from #:random-sample
                #:random-sample)
  (:import-from #:mito
                #:object-id)
  (:import-from #:app/models/user
                #:user-position
                #:user-avatar-url
                #:user-name)
  (:import-from #:serapeum
                #:push-end
                #:fmt
                #:take)
  (:import-from #:alexandria
                #:shuffle)
  (:import-from #:event-emitter
                #:event-emitter)
  (:import-from #:reblocks-ui2/widget
                #:ui-widget)
  (:import-from #:reblocks-ui/form
                #:with-html-form)
  (:import-from #:app/widgets/utils
                #:*green-button-classes*
                #:submit-button)
  (:import-from #:reblocks-ui2/buttons/button
                #:button))
(in-package #:app/games/guess-position)


(defwidget card-widget (event-emitter ui-widget)
  ((user :initarg :user
         :reader user)
   (answers :initarg :answers ;; names
            :reader answers)
   (correct-answer :initarg :correct-answer ;; correct-name
                   :reader correct-answer)
   (answered :initform nil
             :accessor answered)
   (num-attempts-left :initform 5
                      :accessor num-attempts-left)
   (suggest-getter :initarg :suggest-getter
                   :reader suggest-getter
                   :documentation "–§—É–Ω–∫—Ü–∏—è, –ø—Ä–∏–Ω–∏–º–∞—é—â–∞—è —Å–æ—Ç—Ä—É–¥–Ω–∏–∫–∞ –∏ –æ—Ç–¥–∞—é—â–∞—è –ø–æ–¥—Å–∫–∞–∑–∫—É –ø–æ–¥ —Ñ–æ—Ç–æ, –Ω–∞–ø—Ä–∏–º–µ—Ä –¥–æ–ª–∂–Ω–æ—Å—Ç—å –∏–ª–∏ —Ñ–∞–º–∏–ª–∏—é.")
   (failed :initform nil
           :accessor failed
           :documentation "–ü—Ä–æ—Å—Ç–∞–≤–ª—è–µ—Ç—Å—è –≤ True, –∫–æ–≥–¥–∞ –∑–∞–∫–∞–Ω—á–∏–≤–∞—é—Ç—Å—è –ø–æ–ø—ã—Ç–∫–∏."))
  (:documentation "–ö–∞—Ä—Ç–æ—á–∫–∞ —Å —Ñ–æ—Ç–∫–æ–π —Å–æ—Ç—Ä—É–¥–Ω–∏–∫–∞ –∏ –Ω–µ—Å–∫–æ–ª—å–∫–∏–º–∏ –∏–º–µ–Ω–∞–º–∏ –Ω–∞ –≤—ã–±–æ—Ä."))


(defmethod print-object ((obj card-widget) stream)
  (print-unreadable-object (obj stream :type t)
    (format stream "~A attempts: ~A, correct: ~A"
            (correct-answer obj)
            (num-attempts-left obj)
            (answer-correct-p obj))))


(defwidget guess-position-widget (event-emitter ui-widget)
  ((cards :initarg :cards
          :accessor cards)
   (failed-cards :initform nil
                 :accessor failed-cards)
   (successful-cards :initform nil
                     :accessor successful-cards)
   (congratulation-getter :initarg :congratulation-getter
                          :reader congratulation-getter))
  (:documentation "–ò–≥—Ä–∞ –≤ –∫–æ—Ç–æ—Ä–æ–π –ø–æ —Ñ–æ—Ç–æ–≥—Ä–∞—Ñ–∏–∏ –Ω–∞–¥–æ —É–≥–∞–¥—ã–≤–∞—Ç—å –∏–º—è –∫–æ–ª–ª–µ–≥–∏."))


(defun make-guess-position-widget (&key (num-cards 5)
                                        (answer-getter #'user-position)
                                        (suggest-getter #'user-name)
                                        (congratulation-getter
                                         (lambda (&key num-successes &allow-other-keys)
                                           (fmt "–í—ã —É—Å–ø–µ—à–Ω–æ –ø—Ä–æ—à–ª–∏ –∏–≥—Ä—É –∏ –∑–∞–ø–æ–º–Ω–∏–ª–∏ –¥–æ–ª–∂–Ω–æ—Å—Ç–∏ ~A –∫–æ–ª–ª–µ–≥!"
                                                num-successes))))
  (let* ((current-user (get-current-user))
         (all (remove current-user
                      (get-all-users)
                      ;; –ó–∞ –∏—Å–∫–ª—é—á–µ–Ω–∏–µ–º —Ç–µ–∫—É—â–µ–≥–æ —Å–æ—Ç—Ä—É–¥–Ω–∏–∫–∞
                      :key #'object-id))
         (all-answers (mapcar answer-getter
                              all))
         (users (random-sample all num-cards))
         (main-widget (make-instance 'guess-position-widget
                                     :congratulation-getter congratulation-getter))
         (cards
           (flet ((on-next-button (card)
                    ;; –ï—Å–ª–∏ –∫–∞—Ä—Ç–æ—á–∫–∞ –æ—Ç–≤–µ—á–µ–Ω–∞ –Ω–µ–≤–µ—Ä–Ω–æ, —Ç–æ —Å–±—Ä–æ—Å–∏–º –æ—Ç–≤–µ—Ç
                    ;; –∏ –µ—Å–ª–∏ –∑–∞–∫–æ–Ω—á–∏–ª–∏—Å—å –ø–æ–ø—ã—Ç–∫–∏, —Ç–æ –ø–æ–º–µ—Ç–∏–º –∫–∞–∫ –Ω–µ–ø—Ä–æ–π–¥–µ–Ω–Ω—É—é.

                    ;; –í—ã–Ω–µ–º –∫–∞—Ä—Ç–æ—á–∫—É –∏–∑ –æ–±—â–µ–π –∫–æ–ª–æ–¥—ã
                    (setf (cards main-widget)
                          (rest (cards main-widget)))
                                             
                    (cond
                      ((answer-correct-p card)
                       ;; –ò –ø–µ—Ä–µ–ª–æ–∂–∏–º –≤ —É—Å–ø–µ—à–Ω—ã–µ
                       (push card
                             (successful-cards main-widget)))
                      (t
                       (decf (num-attempts-left card))
                       (cond
                         ;; –ï—Å–ª–∏ –ø–æ–ø—ã—Ç–∫–∏ –∏—Å—Ç–µ–∫–ª–∏, —Ç–æ –ø–µ—Ä–µ–ª–æ–∂–∏–º –≤ –Ω–µ—É—Å–ø–µ—à–Ω—ã–µ
                         ((zerop (num-attempts-left card))
                          (setf (failed card)
                                t)
                          (push card
                                (failed-cards main-widget)))
                         ;; –ï—Å–ª–∏ –µ—Å—Ç—å –µ—â—ë –ø–æ–ø—ã—Ç–∫–∏, —Ç–æ –∑–∞—Å—É–Ω–µ–º
                         ;; –∫–∞—Ä—Ç–æ—á–∫—É –≤ –∫–æ–Ω–µ—Ü –∫–æ–ª–æ–¥—ã
                         (t
                          (reset-answer card)
                          (push-end card
                                    (cards main-widget))))))
                    (update main-widget)))
             (loop for user in users
                   for correct-answer = (funcall answer-getter user)
                   for answers = (shuffle
                                  (list* correct-answer
                                         (take 4
                                               (remove correct-answer
                                                       (remove-duplicates
                                                        (random-sample
                                                       ;; –í–æ–∑—å–º–µ–º –∏–º–µ–Ω —Å –∑–∞–ø–∞—Å–æ–º
                                                         all-answers
                                                         10)
                                                        :test 'string-equal)
                                                       :test 'string-equal))))
                   for card = (make-instance 'card-widget
                                             :user user
                                             :answers answers
                                             :suggest-getter suggest-getter
                                             :correct-answer correct-answer)
                   do (event-emitter:on :next-button card
                                        #'on-next-button)
                   collect card))))
    (setf (cards main-widget)
          cards)
    main-widget))


(defmethod render ((widget guess-position-widget))
  (let* ((card (first (cards widget))))
    (with-html
      (:div :class "flex justify-center"
            (cond
              (card
               (render card))
              (t
               (:div :class "flex flex-col gap-8"
                     (:div :class "text-2xl font-bold text-center"
                           "–ü–æ–∑–¥—Ä–∞–≤–ª—è–µ–º!")
                     (:div :class "text-xl font-bold text-center"
                           (funcall (congratulation-getter widget)
                                    :num-successes (length (successful-cards widget))))
                     (:div :class "text-center"
                           :style "font-size: 200px"
                           "üéâ")

                     (let ((game-score (coerce
                                        (ceiling
                                         (float
                                          (/ (* 100 (length
                                                     (successful-cards widget)))
                                             (total-cards widget))))
                                        'integer)))
                       (:div :class "text-center"
                             (render (button "–ü—Ä–æ–¥–æ–ª–∂–∏—Ç—å –æ–±—É—á–µ–Ω–∏–µ!"
                                             :class *green-button-classes*
                                             :on-click (lambda (&rest rest)
                                                         (declare (ignore rest))
                                                         (event-emitter:emit :continue widget
                                                                             game-score)))))))))))))


(defun total-cards (widget)
  (check-type widget guess-position-widget)
  (+ (length (successful-cards widget))
     (length (failed-cards widget))
     (length (cards widget))))


(defun answer-correct-p (widget)
  (check-type widget card-widget)
  (and (answered widget)
       (string-equal (answered widget)
                     (correct-answer widget))))

(defun reset-answer (widget)
  (check-type widget card-widget)
  (setf (answered widget)
        nil))


(defmethod render ((widget card-widget))
  (flet ((on-submit (&key answer &allow-other-keys)
           (log:error "ANSWER" answer)
           (cond
             ;; –ï—Å–ª–∏ –æ—Ç–≤–µ—Ç —É–∂–µ –¥–∞–Ω –∏ –Ω–∞–∂–∞–ª–∏ –∫–Ω–æ–ø–∫—É –î–∞–ª–µ–µ
             ((answered widget)
              (event-emitter:emit :next-button widget
                                  widget))
             ;; –ü–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—å –¥–∞–ª –æ—Ç–≤–µ—Ç –≤–ø–µ—Ä–≤—ã–µ
             (t
              (when answer
                (setf (answered widget)
                      answer))))
           (update widget)))
    
    (with-html-form (:post #'on-submit)
      (:div :class "w-60 h-60 flex flex-col gap-8"
            (:div :class "flex flex-col gap-2"
                  (:img :class "w-full h-full rounded-full border-2 shadow-xl"
                        :src (user-avatar-url (user widget)))
                  (:div :class "font-bold text-center"
                        (funcall (suggest-getter widget)
                                 (user widget))))
            (:div :class "flex flex-col gap-4"
                  (:div :class "flex flex-col gap-2 pl-8"
                        (loop for answer in (answers widget)
                              for class = (cond
                                            ((answered widget)
                                             (cond
                                               ;; –û—Ç–≤–µ—Ç –≤—ã–±—Ä–∞–Ω –ø—Ä–∞–≤–∏–ª—å–Ω–æ
                                               ((and (string-equal (answered widget)
                                                                   answer)
                                                     (string-equal (correct-answer widget)
                                                                   answer))
                                                "font-bold text-green-400")
                                               ;; –û—Ç–≤–µ—Ç –≤—ã–±—Ä–∞–Ω –Ω–µ–≤–µ—Ä–Ω–æ
                                               ((string-equal (answered widget)
                                                              answer)
                                                "text-red-400")
                                               ((string-equal (correct-answer widget)
                                                              answer)
                                                "text-green-400")
                                               (t
                                                "text-gray-400")))
                                            (t
                                             ""))
                              do (:div :class "flex gap-2"
                                       (:input :type "radio"
                                               :name "answer"
                                               :id answer
                                               :value answer
                                               :disabled (answered widget))
                                       (:label :for answer
                                               :class class
                                               answer))))
                  (:div :class "text-center flex flex-col gap-2"
                        (let ((num-attempts (num-attempts-left widget)))
                          (when (and (not (answer-correct-p widget))
                                     (<= num-attempts (if (answered widget)
                                                          5
                                                          4)))
                            (:div :class "text-gray-500"
                                  (if (answered widget)
                                      (case num-attempts
                                        (5 "–û—Å—Ç–∞–ª–æ—Å—å —á–µ—Ç—ã—Ä–µ –ø–æ–ø—ã—Ç–∫–∏")
                                        (4 "–û—Å—Ç–∞–ª–æ—Å—å —Ç—Ä–∏ –ø–æ–ø—ã—Ç–∫–∏")
                                        (3 "–û—Å—Ç–∞–ª–æ—Å—å –¥–≤–µ –ø–æ–ø—ã—Ç–∫–∏")
                                        (2 "–û—Å—Ç–∞–ª–∞—Å—å –ø–æ—Å–ª–µ–¥–Ω—è—è –ø–æ–ø—ã—Ç–∫–∏")
                                        (1 "–í —Å–ª–µ–¥—É—é—â–∏–π —Ä–∞–∑ —Å—Ç–æ–∏—Ç –ø–æ–¥–Ω–∞–ø—Ä—è—á—å—Å—è!")
                                        (0 "–í—Å—ë –ø—Ä–æ–ø–∞–ª–æ!"))
                                      (case num-attempts
                                        (5 "–û—Å—Ç–∞–ª–æ—Å—å –ø—è—Ç—å –ø–æ–ø—ã—Ç–æ–∫")
                                        (4 "–û—Å—Ç–∞–ª–æ—Å—å —á–µ—Ç—ã—Ä–µ –ø–æ–ø—ã—Ç–∫–∏")
                                        (3 "–û—Å—Ç–∞–ª–æ—Å—å —Ç—Ä–∏ –ø–æ–ø—ã—Ç–∫–∏")
                                        (2 "–û—Å—Ç–∞–ª–æ—Å—å –¥–≤–µ –ø–æ–ø—ã—Ç–∫–∏")
                                        (1 "–≠—Ç–æ –ø–æ—Å–ª–µ–¥–Ω—è—è –ø–æ–ø—ã—Ç–∫–∞")
                                        (0 "–í—Å—ë –ø—Ä–æ–ø–∞–ª–æ!"))))))
                        (if (answered widget)
                            (submit-button :text "–°–ª–µ–¥—É—é—â–∞—è –∫–∞—Ä—Ç–æ—á–∫–∞"
                                           :classes *green-button-classes*)
                            (submit-button :text "–ü—Ä–æ–≤–µ—Ä–∏—Ç—å"))))))))


(defmethod get-dependencies ((widget guess-position-widget))
  (call-next-method))
